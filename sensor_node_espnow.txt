#include <Arduino.h>
#include <WiFi.h>
#include <esp_now.h>
#include <Adafruit_MPU6050.h>
#include <Adafruit_Sensor.h>
#include <Wire.h>

#define NODE_ID 1

#define MOTOR_IN1 18
#define MOTOR_IN2 19
#define GREEN_LED 23
#define RED_LED 5

#define VIBRATION_THRESHOLD 60.0

uint8_t gatewayMAC[] = {0xC0, 0xCD, 0xD6, 0xCD, 0xF1, 0xBC};

typedef struct {
  uint8_t nodeId;
  bool isAnomalous;
  float vibrationLevel;
} SensorMessage;

SensorMessage outgoingData;
Adafruit_MPU6050 mpu;

void onDataSent(const uint8_t *mac, esp_now_send_status_t status) {
  Serial.print("Send Status: ");
  Serial.println(status == ESP_NOW_SEND_SUCCESS ? "Success" : "Fail");
}

void setup() {
  Serial.begin(115200);
  delay(2000);

  pinMode(MOTOR_IN1, OUTPUT);
  pinMode(MOTOR_IN2, OUTPUT);
  pinMode(GREEN_LED, OUTPUT);
  pinMode(RED_LED, OUTPUT);

  digitalWrite(MOTOR_IN1, LOW);
  digitalWrite(MOTOR_IN2, LOW);
  digitalWrite(GREEN_LED, LOW);
  digitalWrite(RED_LED, LOW);

  Serial.println("=== Sensor Node " + String(NODE_ID) + " ===");

  WiFi.mode(WIFI_STA);
  WiFi.disconnect();
  delay(100);

  if(esp_now_init() != ESP_OK) {
    Serial.println("ESP-NOW init failed");
    while(1);
  }

  Serial.println("ESP-NOW initialized");

  esp_now_register_send_cb(onDataSent);

  esp_now_peer_info_t peerInfo = {};
  memcpy(peerInfo.peer_addr, gatewayMAC, 6);
  peerInfo.channel = 0;
  peerInfo.encrypt = false;
  peerInfo.ifidx = WIFI_IF_STA;

  if(esp_now_add_peer(&peerInfo) != ESP_OK) {
    Serial.println("Failed to add peer");
    while(1);
  }

  Serial.println("Gateway peer added");

  Wire.begin(22, 21);

  if(!mpu.begin()) {
    Serial.println("MPU6050 not found!");
    digitalWrite(RED_LED, HIGH);
    while(1);
  }

  Serial.println("MPU6050 Found!");

  mpu.setAccelerometerRange(MPU6050_RANGE_8_G);
  mpu.setGyroRange(MPU6050_RANGE_500_DEG);
  mpu.setFilterBandwidth(MPU6050_BAND_21_HZ);

  digitalWrite(MOTOR_IN1, HIGH);
  digitalWrite(MOTOR_IN2, LOW);
  digitalWrite(GREEN_LED, HIGH);

  Serial.println("Node initialized, monitoring...");
}

void loop() {
  static bool connection_lost = false;

  sensors_event_t a, g, temp;
  mpu.getEvent(&a, &g, &temp);

  bool is_all_zero = (a.acceleration.x == 0.00 && a.acceleration.y == 0.00 && a.acceleration.z == 0.00 &&
                      g.gyro.x == 0.00 && g.gyro.y == 0.00 && g.gyro.z == 0.00);

  if(is_all_zero) {
    if(!connection_lost) {
      connection_lost = true;
      Serial.println("MPU6050 connection lost. Stopping motor...");
      digitalWrite(MOTOR_IN1, LOW);
      digitalWrite(MOTOR_IN2, LOW);
      digitalWrite(GREEN_LED, LOW);
      digitalWrite(RED_LED, HIGH);
    }

    if(mpu.begin()) {
      Serial.println("MPU6050 reconnected!");
      mpu.setAccelerometerRange(MPU6050_RANGE_8_G);
      mpu.setGyroRange(MPU6050_RANGE_500_DEG);
      mpu.setFilterBandwidth(MPU6050_BAND_21_HZ);
      connection_lost = false;
      digitalWrite(MOTOR_IN1, HIGH);
      digitalWrite(MOTOR_IN2, LOW);
      digitalWrite(GREEN_LED, HIGH);
      digitalWrite(RED_LED, LOW);
    }
    delay(500);
    return;
  }

  connection_lost = false;

  float vibration_magnitude = sqrt(a.acceleration.x * a.acceleration.x +
                                   a.acceleration.y * a.acceleration.y +
                                   a.acceleration.z * a.acceleration.z);

  bool isAnomalous = vibration_magnitude > VIBRATION_THRESHOLD;

  outgoingData.nodeId = NODE_ID;
  outgoingData.isAnomalous = isAnomalous;
  outgoingData.vibrationLevel = vibration_magnitude;

  esp_now_send(gatewayMAC, (uint8_t*)&outgoingData, sizeof(outgoingData));

  if(isAnomalous) {
    Serial.println("ANOMALY: " + String(vibration_magnitude));
    digitalWrite(GREEN_LED, LOW);
    digitalWrite(RED_LED, HIGH);
  } else {
    Serial.println("Normal: " + String(vibration_magnitude));
    digitalWrite(GREEN_LED, HIGH);
    digitalWrite(RED_LED, LOW);
  }

  delay(2000);
}
