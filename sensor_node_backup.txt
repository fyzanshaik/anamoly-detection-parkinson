#include <Arduino.h>
#include <Adafruit_MPU6050.h>
#include <Adafruit_Sensor.h>
#include <Wire.h>

#define MOTOR_IN1 18
#define MOTOR_IN2 19
#define GREEN_LED 23
#define RED_LED 5

#define VIBRATION_THRESHOLD 60.0

Adafruit_MPU6050 mpu;
bool connection_lost = false;

void setup() {
  Serial.begin(115200);
  delay(2000);
  
  pinMode(MOTOR_IN1, OUTPUT);
  pinMode(MOTOR_IN2, OUTPUT);
  pinMode(GREEN_LED, OUTPUT);
  pinMode(RED_LED, OUTPUT);
  
  digitalWrite(MOTOR_IN1, LOW);
  digitalWrite(MOTOR_IN2, LOW);
  digitalWrite(GREEN_LED, LOW);
  digitalWrite(RED_LED, LOW);
  
  Serial.println("=== Sensor Node Test ===");
  Serial.println("Initializing MPU6050...");
  
  Wire.begin(22, 21);
  
  if (!mpu.begin()) {
    Serial.println("ERROR: Failed to find MPU6050 chip!");
    digitalWrite(RED_LED, HIGH);
    while (1) {
      delay(1000);
    }
  }
  
  Serial.println("SUCCESS: MPU6050 Found!");
  
  mpu.setAccelerometerRange(MPU6050_RANGE_8_G);
  mpu.setGyroRange(MPU6050_RANGE_500_DEG);
  mpu.setFilterBandwidth(MPU6050_BAND_21_HZ);
  
  Serial.println("Starting motor...");
  digitalWrite(MOTOR_IN1, HIGH);
  digitalWrite(MOTOR_IN2, LOW);
  digitalWrite(GREEN_LED, HIGH);
  
  delay(2000);
  Serial.println("Monitoring vibration...");
}

void loop() {
  sensors_event_t a, g, temp;
  mpu.getEvent(&a, &g, &temp);

  bool is_all_zero = (a.acceleration.x == 0.00 && a.acceleration.y == 0.00 && a.acceleration.z == 0.00 &&
                      g.gyro.x == 0.00 && g.gyro.y == 0.00 && g.gyro.z == 0.00);

  if (is_all_zero) {
    if (!connection_lost) {
      connection_lost = true;
      Serial.println("MPU6050 connection lost. Stopping motor...");
      digitalWrite(MOTOR_IN1, LOW);
      digitalWrite(MOTOR_IN2, LOW);
      digitalWrite(GREEN_LED, LOW);
      digitalWrite(RED_LED, HIGH);
    }

    if (mpu.begin()) {
      Serial.println("SUCCESS: Reconnected to MPU6050!");
      mpu.setAccelerometerRange(MPU6050_RANGE_8_G);
      mpu.setGyroRange(MPU6050_RANGE_500_DEG);
      mpu.setFilterBandwidth(MPU6050_BAND_21_HZ);
      connection_lost = false;
      digitalWrite(MOTOR_IN1, HIGH);
      digitalWrite(MOTOR_IN2, LOW);
      digitalWrite(GREEN_LED, HIGH);
      digitalWrite(RED_LED, LOW);
    }
    delay(500);
    return;
  }
  
  connection_lost = false;

  float vibration_magnitude = sqrt(a.acceleration.x * a.acceleration.x + 
                                   a.acceleration.y * a.acceleration.y + 
                                   a.acceleration.z * a.acceleration.z);

  Serial.print("Accel: ");
  Serial.print(a.acceleration.x, 2);
  Serial.print(", ");
  Serial.print(a.acceleration.y, 2);
  Serial.print(", ");
  Serial.print(a.acceleration.z, 2);
  Serial.print(" | Magnitude: ");
  Serial.print(vibration_magnitude, 2);

  if (vibration_magnitude > VIBRATION_THRESHOLD) {
    Serial.println(" | STATUS: ANOMALY DETECTED");
    digitalWrite(GREEN_LED, LOW);
    digitalWrite(RED_LED, HIGH);
    digitalWrite(MOTOR_IN1, LOW);
    digitalWrite(MOTOR_IN2, LOW);
  } else {
    Serial.println(" | STATUS: Normal");
    digitalWrite(GREEN_LED, HIGH);
    digitalWrite(RED_LED, LOW);
    digitalWrite(MOTOR_IN1, HIGH);
    digitalWrite(MOTOR_IN2, LOW);
  }
  
  delay(500);
}